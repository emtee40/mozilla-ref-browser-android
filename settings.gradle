include ':app'

final String settingAppServicesPath = "substitutions.application-services.dir"
final String settingAndroidComponentsPath = "substitutions.android-components.dir"

Properties localProperties = null

// FIXME: substitution configuration for android-components.
// See https://github.com/mozilla-mobile/reference-browser/issues/365

if (file('local.properties').canRead()) {
    localProperties = new Properties()
    localProperties.load(file('local.properties').newDataInputStream())
    logger.lifecycle('Local configuration: loaded local.properties')
} else {
    logger.lifecycle('Local configuration: absent local.properties; proceeding as normal.')
}

if (localProperties != null) {
    ////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
    // Substituting application services
    ////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
    final String appServicesLocalPath = localProperties.getProperty(settingAppServicesPath)

    if (appServicesLocalPath != null) {
        logger.lifecycle("Local configuration: substituting application-services modules from path: $appServicesLocalPath")

        includeBuild(appServicesLocalPath) {
            dependencySubstitution {
                substitute module('org.mozilla.appservices:fxaclient') with project(':fxa-client-library')
                substitute module('org.mozilla.appservices:logins') with project(':logins-library')
                substitute module('org.mozilla.appservices:places') with project(':places-library')
                substitute module('org.mozilla.appservices:rustlog') with project(':rustlog-library')
            }
        }
    } else {
        logger.lifecycle("Local configuration: application-services substitution path missing. Specify it via '$settingAppServicesPath' setting.")
    }

    ////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
    // Substituting android components
    ////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
    final String androidComponentsPath = localProperties.getProperty(settingAndroidComponentsPath)

    if (androidComponentsPath != null) {
        logger.lifecycle("Local configuration: substituting android-components modules from path: $androidComponentsPath")

        includeBuild(androidComponentsPath) {
            dependencySubstitution {
                substitute module('org.mozilla.components:concept-engine') with project(':concept-engine')
                substitute module('org.mozilla.components:feature-session') with project(':feature-session')
                substitute module('org.mozilla.components:browser-engine-gecko-nightly') with project(':browser-engine-gecko-nightly')
            }
        }
    } else {
        logger.lifecycle("Local configuration: no android-components substitution via '$settingAndroidComponentsPath' set up.")
    }
}
